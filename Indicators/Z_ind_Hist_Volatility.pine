// This work is licensed under a Attribution-NonCommercial-ShareAlike 4.0 International (CC BY-NC-SA 4.0) https://creativecommons.org/licenses/by-nc-sa/4.0/
// Â© rhapsodyz

//@version=5
indicator(title     = "[Z] Historical Volatility", 
     shorttitle     = "HV", 
     format         = format.price, 
     precision      = 2, 
     timeframe      = "", 
     timeframe_gaps = true)

// ##################
// === Parameters ===
// ##################

length      = input.int(10, minval = 1, title = "Length", group = "HV settings")
lengthEMA   = input.int(100, minval = 1, maxval = 4999, title = "EMA Length", group = "HV settings")
src         = input.source(hlc3, title = "HV Source", group = "HV settings")
multBoll    = input.float(2.0, minval=0.001, maxval=50, title="Boll StdDev", group="Boll Settings")

// ##################
// === Indicators ===
// ##################

annual  = 365
per     = timeframe.isintraday or timeframe.isdaily and timeframe.multiplier == 1 ? 1 : 7
hv      = 100 * ta.stdev(math.log(src / src[1]), length) * math.sqrt(annual / per)
hvEMA   = ta.ema(hv, lengthEMA)
hvDiff  = hv - hvEMA
hvDiv   = (hv - hvEMA) / (ta.stdev(hv, lengthEMA) * multBoll)

hvDivPlot = plot(hvDiv, "HV", color = color.blue)
band1 = hline(0.8, "Hot Market", color=#787B86, linestyle=hline.style_dashed)
hline(0, "Middle Band", color=color.new(#787B86, 50))
band0 = hline(-0.8, "Cold Market", color=#787B86, linestyle=hline.style_dashed)
fill(band1, band0, color=color.rgb(38, 166, 154, 90), title="Background")

midLinePlot = plot(0, color = na, editable = false, display = display.none)
fill(hvDivPlot, midLinePlot, 1.2, 0.8, top_color = color.new(color.green, 0), bottom_color = color.new(color.green, 100),  title = "Overbought Gradient Fill")
fill(hvDivPlot, midLinePlot, -0.8,  -1.2,  top_color = color.new(color.red, 100), bottom_color = color.new(color.red, 0),      title = "Oversold Gradient Fill")
